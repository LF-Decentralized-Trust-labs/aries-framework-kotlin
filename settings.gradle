pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

ext["githubUsername"] = null
ext["githubToken"] = null

def secretPropsFile = file("local.properties")
if (secretPropsFile.exists()) {
    Properties properties = new Properties()
    secretPropsFile.withInputStream { properties.load(it) }
    properties.each { k, v -> ext[k] = v }
} else {
    ext["githubUsername"] = System.getenv("GITHUB_ACTOR")
    ext["githubToken"] = System.getenv("GITHUB_TOKEN")
}

def getExtraString(name) {
    try {
        return ext[name]
    } catch (ignored) {
        return ""
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven {
            setUrl("https://maven.pkg.github.com/hyperledger/aries-uniffi-wrappers")
            credentials {
                username = getExtraString("githubUsername")
                password = getExtraString("githubToken")
            }
        }
        maven { url 'https://jitpack.io' }
    }
}

rootProject.name = "aries-framework-kotlin"
include ':app'
include ':ariesframework'
